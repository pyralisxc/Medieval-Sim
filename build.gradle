plugins {
    id 'java'
}
// Change the values below this to your mods values
// To change the project name, edit the settings.gradle file

project.ext.modID = "medieval.sim" // The unique id of your mod. Must be all lowercase and cannot use special characters.
project.ext.modName = "Medieval Sim" // The display name of your mod.
project.ext.modVersion = "1.0" // Your current builds version. Must follow the xx.xx... format.
project.ext.gameVersion = "1.0.3" // The target game version.
project.ext.modDescription = "Turn your world into a medieval rpg, where you can shape the world, create plots, protected and pvp ares etc." // Short description of what your mod is.
project.ext.author = "Pyralis" // Your name

/**
 * When setting clientside to true, it means servers do not need this mod for clients to connect and vice versa.
 * IMPORTANT: If you set this to true, make sure that your mod does not add any content or do anything
 * that could cause clients and servers to desync. This includes registering any items, objects, tiles, packets etc.
 */
project.ext.clientside = false

/**
 * The other mod dependencies of this mod
 * Dependencies define the default load order of mods
 * Uncomment and configure these if your mod has dependencies
 */
//project.ext.modDependencies = ["other.modid1", "other.modid2"]
//project.ext.modOptionalDependencies = ["optional.modid1", "optional.modid2"]

// The path to the games install directory
def gameDirectory = "C:/Program Files (x86)/Steam/steamapps/common/Necesse"

// =================================================
// ========== DO NOT EDIT BELOW THIS LINE ==========
// =================================================

// Name of the jar
def jarName = "${project.ext.modName.replace(" ", "")}-${project.ext.gameVersion}-${project.ext.modVersion}"

group project.ext.modID
version project.ext.modVersion

setSourceCompatibility(JavaVersion.VERSION_17)
setTargetCompatibility(JavaVersion.VERSION_17)

repositories {
    mavenCentral()
}

configurations {
    libDepends
}

sourceSets.main.output.resourcesDir = file("build/mod/resources/")
sourceSets.main.java.outputDir = file("build/mod/")
sourceSets.main.compileClasspath += configurations.libDepends // Adds libDepends configuration to classpath
def buildLocation = "build/jar/"

compileJava.options.encoding = "UTF-8"

if (!file(gameDirectory + "/Necesse.jar").exists()) {
    throw new Exception("Could not find game install directory. Make sure it is correct in build.gradle file.")
}

dependencies {
    implementation files(gameDirectory + "/Necesse.jar")
    implementation fileTree(gameDirectory + "/lib/")
    implementation fileTree("./mods/") // Add all mods located in local mods folder

    // Add any third party library dependencies here. Remember to use libDepends, so that they will be added to your jar on build
    // These are some examples:
//    libDepends group: 'com.google.guava', name: 'guava', version: '31.1-jre'
//    libDepends files("path/to/library/jar.jar")
}

task createAppID {
    group "necesse"
    description "Creates steam_appid.txt file"

    doLast {
        file("steam_appid.txt").text = "1169040"
    }
}

/**
 * Pre-antialias textures helper
 * Usage examples:
 *  ./gradlew preAntialias -Pfolders="src/main/resources/tiles;src/main/resources/objects"
 *  ./gradlew preAntialias -Pfiles="src/main/resources/tiles/grass.png"
 */
task preAntialias(type: JavaExec) {
    group "necesse"
    description "Run PreAntialiasTextures on provided folders/files"

    classpath = files(gameDirectory + "/Necesse.jar")
    main = "PreAntialiasTextures"

    doFirst {
        def argsList = []
        if (project.hasProperty('folders')) {
            argsList += ['-folders', project.property('folders')]
        }
        if (project.hasProperty('files')) {
            argsList += ['-files', project.property('files')]
        }
        if (argsList.isEmpty()) {
            throw new GradleException('preAntialias requires -Pfolders or -Pfiles to be set')
        }
        args = argsList
    }
}

task createModInfoFile(type: JavaExec) {
    group "necesse"
    description "Creates the mod info file"

    classpath = files(gameDirectory + "/Necesse.jar")

    main = "CreateModInfoFile"
    args "-file", "${sourceSets.main.java.outputDir}/mod.info",
            "-id", "${project.ext.modID}",
            "-name", "${project.ext.modName}",
            "-version", "${project.ext.modVersion}",
            "-gameVersion", "${project.ext.gameVersion}",
            "-description", "${project.ext.modDescription}",
            "-author", "${project.ext.author}",
            "-clientside", "${project.ext.clientside}",
            "-dependencies", project.ext.has("modDependencies") ? "[" + project.ext.modDependencies.join(", ") + "]" : "",
            "-optionalDependencies", project.ext.has("modOptionalDependencies") ? "[" + project.ext.modOptionalDependencies.join(", ") + "]" : ""
}

// Task to build and run the tools ListPackets utility
task buildTools(type: GradleBuild) {
    dir = 'tools'
    tasks = ['build']
}

task runListPackets(type: JavaExec) {
    group 'tools'
    description 'Run the reflection-based packet scanner against build/mod'
    dependsOn buildTools, classes

    doFirst {
        def toolsJar = file('tools/build/libs/list-packets.jar')
        if (!toolsJar.exists()) {
            throw new GradleException('Tools jar not found; run buildTools first')
        }
        def necJar = file(gameDirectory + '/Necesse.jar')
        if (!necJar.exists()) {
            throw new GradleException('Necesse.jar not found in gameDirectory: ' + gameDirectory)
        }
        classpath = files(toolsJar, necJar, file('build/mod'))
        main = 'tools.ListPackets'
        args = [ 'build/mod' ]
    }
}
// Makes compiling also create mod info file
classes.dependsOn("createModInfoFile")

task runClient(type: JavaExec) {
    group "necesse"
    description "Run client with current mod"
    dependsOn "modValidate", "buildModJar", "createAppID"

    classpath = files(gameDirectory + "/Necesse.jar")

    main = "StartSteamClient"
    jvmArgs "-Xms1G", "-Xmx6G", "-XX:+UnlockExperimentalVMOptions", "-XX:+UseG1GC", "-XX:G1NewSizePercent=20", "-XX:G1ReservePercent=20", "-XX:MaxGCPauseMillis=50", "-XX:G1HeapRegionSize=32M"
    args "-dev", "-mod \"${buildLocation}\""
}

task runDevClient(type: JavaExec) {
    group "necesse"
    description "Run client with current mod"
    dependsOn "modValidate", "buildModJar", "createAppID"

    classpath = files(gameDirectory + "/Necesse.jar")

    main = "StartSteamClient"
    jvmArgs "-Xms512m", "-Xmx4G", "-XX:+UnlockExperimentalVMOptions", "-XX:+UseG1GC", "-XX:G1NewSizePercent=20", "-XX:G1ReservePercent=20", "-XX:MaxGCPauseMillis=50", "-XX:G1HeapRegionSize=32M"
    args "-dev 1", "-mod \"${buildLocation}\""
}

task runServer(type: JavaExec) {
    group "necesse"
    description "Run server with current mod"
    dependsOn "modValidate", "buildModJar"

    classpath = files(gameDirectory + "/Server.jar")

    main = "StartDesktopServer"
    jvmArgs "-Xms512m", "-Xmx4G", "-XX:+UnlockExperimentalVMOptions", "-XX:+UseG1GC", "-XX:G1NewSizePercent=20", "-XX:G1ReservePercent=20", "-XX:MaxGCPauseMillis=50", "-XX:G1HeapRegionSize=32M"
    args "-mod \"${buildLocation}\""
}

/**
 * Simple validation task to catch common misconfiguration early
 */
task modValidate {
    group "necesse"
    description "Validate gameDirectory, Necesse jars and mod metadata before running builds/runs"

    doLast {
        def gd = file(gameDirectory)
        if (!gd.exists()) {
            throw new GradleException("gameDirectory does not exist: " + gameDirectory)
        }
        def clientJar = file(gameDirectory + "/Necesse.jar")
        def serverJar = file(gameDirectory + "/Server.jar")
        if (!clientJar.exists() && !serverJar.exists()) {
            throw new GradleException("Could not find Necesse.jar or Server.jar in gameDirectory: " + gameDirectory)
        }
        if (!project.ext.has("modID") || project.ext.modID.toString().trim() == "") {
            throw new GradleException("modID not set in build.gradle")
        }
        println "modValidate: OK (gameDirectory and modID present)"
    }
}

task buildModJar(type: Jar) {
    group "necesse"
    description "Generates the mod jar into the build folder"
    dependsOn "classes"

    doFirst {
        fileTree(dir: buildLocation).exclude("${jarName}.jar").visit { FileVisitDetails details ->
            delete details.file
        }
    }

    // Add mod output
    from sourceSets.main.java.outputDir
    // Add the dependencies
    from configurations.libDepends.collect { it.isDirectory() ? it : zipTree(it) }

    archiveName "${jarName}.jar"
    destinationDir file(buildLocation)
}